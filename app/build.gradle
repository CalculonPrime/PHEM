apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    ndkVersion "17.2.4988734"

    defaultConfig {
        applicationId "com.perpendox.phem"
        minSdkVersion 9
        targetSdkVersion 28

        versionCode 2190144
        versionName "1.44"
        
        flavorDimensions "architecture"

        productFlavors {
            arm {
                versionNameSuffix "-arm"
                dimension "architecture"
                ndk {
                    abiFilters "armeabi-v7a"
                }
            }
            intel {
                versionNameSuffix "-intel"
                dimension "architecture"
                ndk {
                    abiFilters "x86"
                }
            }
        }

        externalNativeBuild {
            ndkBuild {
                arguments "NDK_APPLICATION_MK=src/main/jni/Application.mk"
            }
        }
    }
    
    signingConfigs {
        release {
            storeFile rootProject.file('release.keystore')
            storePassword System.getenv('KEYSTORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            if (rootProject.file('release.keystore').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.android.support:support-v4:25.3.1'
    implementation 'com.android.support:appcompat-v7:25.3.1'
}
